/datum/unit_test/open_air/Run()
	for(var/turf/open/T as anything in reservation.reserved_turfs)
		T.Initalize_Atmos(0)
		T.air.clear()
	var/turf/open/center_turf = locate(run_loc_floor_bottom_left.x+2,run_loc_floor_bottom_left.y+2,run_loc_floor_bottom_left.z)
	center_turf.air.set_moles(GAS_PLASMA, 32)
	for(var/i in 1 to 10)
		SSair.fire()
	if(center_turf.air.get_moles(GAS_PLASMA) > 28)
		Fail("Gas isn't moving at all, or isn't moving enough (somehow)")
	center_turf.air.set_moles(GAS_PLASMA, 100)
	center_turf.air.set_moles(GAS_O2, 100/1.4)
	center_turf.air.set_temperature(400)
	center_turf.air.vv_react(center_turf)
	if(center_turf.air.get_moles(GAS_PLASMA) >= 95)
		Fail("Gas isn't reacting properly")
	if(center_turf.air.return_temperature() <= 400)
		Fail("Gas temperature isn't changing properly with air reactions")
	var/obj/effect/hotspot = locate(/obj/effect/hotspot) in center_turf
	if(!istype(hotspot))
		Fail("Hotspots aren't showing up on reaction")

/datum/unit_test/open_air/Destroy()
	for(var/turf/T as anything in reservation.reserved_turfs)
		T.Initalize_Atmos(0)
	..()
